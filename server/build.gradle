plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.2'
	id 'io.spring.dependency-management' version '1.1.5'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

ext.jaxbSourceDir = "${buildDir}/generated/sources/jaxb"

repositories {
	mavenCentral()
}

configurations {
	jaxb
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web-services'
	implementation 'wsdl4j:wsdl4j'
	jaxb("org.glassfish.jaxb:jaxb-xjc")

	implementation 'org.springframework.ws:spring-ws-security:latest.release'
}

sourceSets {
	main {
		java {
			srcDir 'src/main/java'
			srcDir jaxbSourceDir
		}
	}
}

bootJar {
	archiveBaseName = 'okomes'
	archiveVersion = '0.0.1'
}

task genJaxb {
	ext.schema = "src/main/resources/okomes.xsd"

	outputs.dir jaxbSourceDir

	doLast() {
		project.ant {
			taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask", classpath: configurations.jaxb.asPath
			mkdir(dir: jaxbSourceDir)

			xjc(destdir: jaxbSourceDir, schema: schema) {
				arg(value: "-wsdl")
				produces(dir: jaxbSourceDir, includes: "**/*.java")
			}
		}
	}
}

compileJava.dependsOn genJaxb
